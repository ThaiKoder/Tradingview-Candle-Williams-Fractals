//@version=6
indicator("Candle Williams Fractals", shorttitle="CDL Fractals", format=format.price, precision=0, overlay=true)
// Define "n" as the number of periods and keep a minimum value of 2 for error handling.
n = input.int(title="Periods", defval=2, minval=2)


var float lastHigh = na
var float lastLow  = na


// UpFractal
bool upflagDownFrontier = true
bool upflagUpFrontier0 = true
bool upflagUpFrontier1 = true
bool upflagUpFrontier2 = true
bool upflagUpFrontier3 = true
bool upflagUpFrontier4 = true

//high
for i = 1 to n
    upflagDownFrontier := upflagDownFrontier and (close[n-i] < close[n])
    upflagUpFrontier0 := upflagUpFrontier0 and (close[n+i] < close[n])
    upflagUpFrontier1 := upflagUpFrontier1 and (close[n+1] <= close[n] and close[n+i + 1] < close[n])
    upflagUpFrontier2 := upflagUpFrontier2 and (close[n+1] <= close[n] and close[n+2] <= close[n] and close[n+i + 2] < close[n])
    upflagUpFrontier3 := upflagUpFrontier3 and (close[n+1] <= close[n] and close[n+2] <= close[n] and close[n+3] <= close[n] and close[n+i + 3] < close[n])
    upflagUpFrontier4 := upflagUpFrontier4 and (close[n+1] <= close[n] and close[n+2] <= close[n] and close[n+3] <= close[n] and close[n+4] <= close[n] and close[n+i + 4] < close[n])
flagUpFrontier = upflagUpFrontier0 or upflagUpFrontier1 or upflagUpFrontier2 or upflagUpFrontier3 or upflagUpFrontier4

upFractal = (upflagDownFrontier and flagUpFrontier)


// downFractal
bool downflagDownFrontier = true
bool downflagUpFrontier0 = true
bool downflagUpFrontier1 = true
bool downflagUpFrontier2 = true
bool downflagUpFrontier3 = true
bool downflagUpFrontier4 = true

//low
for i = 1 to n
    downflagDownFrontier := downflagDownFrontier and (close[n-i] > close[n])
    downflagUpFrontier0 := downflagUpFrontier0 and (close[n+i] > close[n])
    downflagUpFrontier1 := downflagUpFrontier1 and (close[n+1] >= close[n] and close[n+i + 1] > close[n])
    downflagUpFrontier2 := downflagUpFrontier2 and (close[n+1] >= close[n] and close[n+2] >= close[n] and close[n+i + 2] > close[n])
    downflagUpFrontier3 := downflagUpFrontier3 and (close[n+1] >= close[n] and close[n+2] >= close[n] and close[n+3] >= close[n] and close[n+i + 3] > close[n])
    downflagUpFrontier4 := downflagUpFrontier4 and (close[n+1] >= close[n] and close[n+2] >= close[n] and close[n+3] >= close[n] and close[n+4] >= close[n] and close[n+i + 4] > close[n])
flagDownFrontier = downflagUpFrontier0 or downflagUpFrontier1 or downflagUpFrontier2 or downflagUpFrontier3 or downflagUpFrontier4

downFractal = (downflagDownFrontier and flagDownFrontier)

if upFractal
    string txt = na(lastHigh) ? "H" : close[n] > lastHigh ? "HH" : "LH"
    label.new(
        bar_index - n,
        close[n],
        txt,
        style=label.style_label_down,
        textcolor=color.white,
        color=txt == "HH" ? color.green : color.red,
        size=size.small
    )
    lastHigh := close[n]


if downFractal
    string txt = na(lastLow) ? "L" : close[n] < lastLow ? "LL" : "HL"
    label.new(
        bar_index - n,
        close[n],
        txt,
        style=label.style_label_up,
        textcolor=color.white,
        color=txt == "LL" ? color.red : color.green,
        size=size.small
    )
    lastLow := close[n]
